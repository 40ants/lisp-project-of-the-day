<html><body><h1 class='title'>cl-custom-hash-table</h1>
In Common Lisp standard, a hash table can use only these test functions
to check keys equality: eq, eql, equal and equalp
(https://eli.thegreenplace.net/2004/08/08/equality-in-lisp), however
sometimes you might want to provide a custom comparison and hash
functions.

Some implementations support this by providing an
extension. Cl-custom-hash-table provides a portability layer for
defining such custom hash functions.

BTW, did you know there is a table of Common Lisp portability layers?

https://shinmera.github.io/portability/

Working on this post, I&#039;ve created an issue to add cl-custom-hash-table
into this list.

Let&#039;s see, how does SBCL allow you to define custom hash functions using
sb-ext:define-hash-table-test macro.

For example, we want a hash table where we can use the first character
of a string, given as a key.

First, we need to define a comparison function:

<pre><code>
POFTHEDAY&gt; (defun first-char-equal (left right)
             (char-equal (elt left 0)
                         (elt right 0)))
FIRST-CHAR-EQUAL

POFTHEDAY&gt; (first-char-equal &quot;Foo&quot; &quot;foo&quot;)
T
POFTHEDAY&gt; (first-char-equal &quot;Foo&quot; &quot;Bar&quot;)
NIL
</code></pre>

Please, note, here I&#039;ve sacrificed the processing of corner cases when
left and right are empty or not a strings in favor of readability.

Also, we need to define a hash-function:

<pre><code>
POFTHEDAY&gt; (defun first-char-hash (text)
             (sxhash (char-downcase (elt text 0))))
FIRST-CHAR-HASH

POFTHEDAY&gt; (first-char-hash &quot;foo&quot;)
1193941380876393778 (61 bits, #x1091BB5C31C6DD32)

POFTHEDAY&gt; (first-char-hash &quot;Foo&quot;)
1193941380876393778 (61 bits, #x1091BB5C31C6DD32)
</code></pre>

Now we can to register our custom hash function and to create a
hash-table:


<pre><code>
POFTHEDAY&gt; (sb-ext:define-hash-table-test first-char-equal first-char-hash)
FIRST-CHAR-EQUAL

POFTHEDAY&gt; (defparameter *h* (make-hash-table :test &#039;first-char-equal))
*H*

POFTHEDAY&gt; (setf (gethash &quot;Foo&quot; *h*)
                 :BAR)
:BAR

POFTHEDAY&gt; (setf (gethash &quot;fooga-dooga&quot; *h*)
                 :ANOTHER)
:ANOTHER

POFTHEDAY&gt; (setf (gethash &quot;second&quot; *h*)
                 :VALUE)
:VALUE

POFTHEDAY&gt; (maphash (lambda (key value)
                      (format t &quot;~A -&gt; ~A~%&quot;
                              key
                              value))
                    *h*)
Foo -&gt; ANOTHER
second -&gt; VALUE
</code></pre>

As you can see, this works as expected.

Now let&#039;s try to define the same hash table using cl-custom-hash-table:

<pre><code>
POFTHEDAY&gt; (cl-custom-hash-table:define-custom-hash-table-constructor
               make-first-char-hash
             :test first-char-equal
             :hash-function first-char-hash)
MAKE-FIRST-CHAR-HASH  

POFTHEDAY&gt; (defparameter *h* (make-first-char-hash))
*H*
POFTHEDAY&gt; (cl-custom-hash-table:with-custom-hash-table
             (setf (gethash &quot;Foo&quot; *h*)
                   :BAR)
             (setf (gethash &quot;fooga-dooga&quot; *h*)
                   :ANOTHER)
             (setf (gethash &quot;second&quot; *h*)
                   :VALUE)
             (maphash (lambda (key value)
                      (format t &quot;~A -&gt; ~A~%&quot;
                              key
                              value))
                    *h*))
Foo -&gt; ANOTHER
second -&gt; VALUE</code></pre>

As you can see, the result is the same. However, we have to wrap our code
into the cl-custom-hash-table:with-custom-hash-table macro call. We need
this to ensure that correct functions will be used on Lisp
implementation which doesn&#039;t support the hash function&#039;s customization.
</body></html>